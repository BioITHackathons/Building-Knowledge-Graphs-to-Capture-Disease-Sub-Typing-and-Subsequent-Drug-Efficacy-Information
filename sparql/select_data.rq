PREFIX cvo:  <http://purl.jp/bio/10/clinvar/>
PREFIX dct:  <http://purl.org/dc/terms/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX sio:    <http://semanticscience.org/resource/>

SELECT * 
WHERE { 
   SERVICE <https://grch38.togovar.org/sparql> {
       SELECT *
       WHERE {
            VALUES ?tgv_id { "tgv57846983" }

            GRAPH <http://togovar.biosciencedbc.jp/variant> {
                ?variant dct:identifier ?tgv_id .
            }
            
            GRAPH <http://togovar.biosciencedbc.jp/variant/annotation/clinvar> {
            ?variant dct:identifier ?variation_id .
            BIND(IRI(CONCAT("http://ncbi.nlm.nih.gov/clinvar/variation/", ?variation_id)) AS ?clinvar)
        }
            
        GRAPH <http://togovar.biosciencedbc.jp/clinvar> {
            ?clinvar a cvo:VariationArchiveType ;
                rdfs:label ?title ;
                cvo:accession ?vcv ;
                cvo:interpreted_record ?interpreted_record .
            ?interpreted_record cvo:review_status ?review_status ;
                 cvo:rcv_list ?_rcv_list ;
                 sio:SIO_000628 ?sio_accession .
            ?_rcv_list cvo:rcv_accession ?_rcv .
            ?sio_accession dct:references ?dbsnp . 
            ?dbsnp rdfs:seeAlso ?rs_id .
            ?_rcv cvo:interpretation ?interpretation ;
                  cvo:date_last_evaluated ?last_evaluated ;
                cvo:interpreted_condition_list ?_ic_list .
            ?_ic_list cvo:interpreted_condition ?_interpreted_condition .
                ?_interpreted_condition rdfs:label ?condition .

    OPTIONAL {
      ?_interpreted_condition dct:source ?db ;
        dct:identifier ?medgen .
      FILTER(?db IN ("MedGen"))
    }

    }       
}
    }
}
    
